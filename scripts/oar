#!/bin/csh

# ----------------------------------------
# Launcher for: Offline Admin Relay (OAR).
# ----------------------------------------

if (${#argv} < 1) then
    echo "\033[45mUsage: oar ( start | stop ) <scope> \033[0m"
    exit 1
endif

source ${DEPLOY_BIN}/setup
source ${DEPLOY_BIN}/class_setup
source ${DEPLOY_BIN}/oar_setup

set model = $2

# Add arg list.
source $OAR_CONFIG/${model}_oar_args 

set PID = $DEPLOY_TMP/oar_${model}.pid

onintr cleanup

switch ($1)

    case stop:

	echo "Offline Admin Relay: (${model}) stopping"
	
	if (! -e $PID) then
	    echo "Cannot locate a PID file: $PID "
	    exit 1
	endif
	set AID = `cat $PID` 
    
	if ($AID == "") then
	    echo "$PID does not contain a valid PID for the OAR"
	    exit 2
	endif

	set BID = `ps -ef | grep $AID | awk '{if ($3 == '$AID') print $2}'`
	echo OAR Watchdog process is $AID ..Killing with SIG_KILL
	kill -9 $AID  
	echo PCR JVM Process is      $BID ..Killing with SIG_KILL
	kill -9 $BID

	rm -f $PID

	breaksw

    case start:

	echo "Offline Admin Relay: (${model}) starting"

	echo $$ > $PID
	
	cd $OAR_HOME

	# Check certificate validity.
	#$POS_SCRIPTS/check_cert $KEYSTORE $KEYPASS
	#$POS_SCRIPTS/check_cert $TRUSTSTORE public

	set EGD = "-Djava.security.egd=file:/dev/urandom"
	set DEBUG

	set KS = "-Djavax.net.ssl.keyStore=${KEYSTORE}"
	set KP = "-Djavax.net.ssl.keyStorePassword=${KEYPASS}"
	set TS = "-Djavax.net.ssl.trustStore=${TRUSTSTORE}"

	$JAVA -DOAR  $EGD $DEBUG $KS $KP $TS ProxyServer ${OAR_CONFIG}/${model}_oar.properties

	set mexit = $status
	echo "OAR (${model}) Process exited with status $mexit "

	rm -f $PID

	exit 0

	breaksw

endsw

cleanup:
rm -f $PID
