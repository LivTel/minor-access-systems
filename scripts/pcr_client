#!/bin/csh

# ------------------------------------------------------------
# Launcher for: Planetarium Control Relay Client (PCR_Client).  
# ------------------------------------------------------------

source $DEPLOY_BIN/setup
source $DEPLOY_BIN/class_setup
source $DEPLOY_BIN/pcr_setup

# pcr_client <route> <rtoc> 

if (${#argv} < 2) then
    echo "Usage: pcr_client <route> <rtoc>"
    echo "       route: Route setup."
    foreach route ($PCR_CONFIG/*_pcrclient_args)
	set rt  = ${route:t}
	echo "             - $rt "
    end
    echo "       rtoc:  RTOC whose certificate to use."
    exit 1
endif

set model = $1
set rtoc  = $2

source $PCR_CONFIG/${model}_pcrclient_args

echo "PCR Client startup: $model "

# Check we have the key store (server's private key).

set kfile = $PCR_CERTS/${rtoc}.private
if (-e $kfile) then
	set KEYSTORE = $kfile
endif

if (! -e $KEYSTORE) then   
    echo "Could not find Keystore: $KEYSTORE" 
    exit 1
endif

# Check we have the trust store (clients' public keys).
if (! -e $TRUSTSTORE) then
    echo "Could not find Truststore: $TRUSTSTORE"
    exit 2
endif

set EGD = -Djava.security.egd=file:/dev/urandom

set KS = "-Djavax.net.ssl.keyStore=${KEYSTORE}"
set KP = "-Djavax.net.ssl.keyStorePassword=${KEYPASS}"
set TS = "-Djavax.net.ssl.trustStore=${TRUSTSTORE}"

java  $DEBUG $EGD $KS $KP $TS POSSocketClient $HOST $PORT secure
