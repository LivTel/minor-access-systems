#!/bin/csh

# ------------------------------------------
# Launcher for: Data Generator (GEN).
# ------------------------------------------

if (${#argv} < 2) then
    echo "\033[45mUsage: gen ( start | stop ) <model> \033[0m"
    exit 1
endif

source ${DEPLOY_BIN}/setup
source ${DEPLOY_BIN}/class_setup
source ${DEPLOY_BIN}/gen_setup

echo $CLASSPATH 

set model = $2

# Add arg list.
source $GEN_CONFIG/${model}_gen_args 

set PID = $DEPLOY_TMP/gen_${model}.pid

switch ($1)

    case stop:

	echo "Data Generator: (${model}) stopping"
	
	if (! -e $PID) then
	    echo "Cannot locate a PID file: $PID "
	    exit 1
	endif
	set AID = `cat $PID` 
    
	if ($AID == "") then
	    echo "$PID does not contain a valid PID for the GEN"
	    exit 2
	endif

	set BID = `ps -ef | grep $AID | awk '{if ($3 == '$AID') print $2}'`
	echo GEN Watchdog process is $AID ..Killing with SIG_KILL
	kill -9 $AID  
	echo GEN Process is          $BID ..Killing with SIG_KILL
	kill -9 $BID

	rm -f $PID

	breaksw

    case start:

	echo "Data Generator: (${model}) starting"

	echo $$ > $PID

	if ( ! $?SCRIPT ) then
	    echo "Data Generator: (${model})  ERROR: Script is not defined in $GEN_CONFIG/${model}_gen_args "
	    exit 1
	else
	    $SCRIPT
	endif

	set mexit = $status

	echo "GEN (${model}) Process exited with status $mexit "

	rm -f $PID

	breaksw

endsw
