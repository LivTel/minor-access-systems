#!/bin/csh

# --------------------------------------------
# Launcher for: Data Generator Watchdog (DGW).
# --------------------------------------------

if (${#argv} < 2) then
    echo "\033[45mUsage: dgw ( start | stop ) <model> \033[0m"
    exit 1
endif

source ${DEPLOY_BIN}/setup
source ${DEPLOY_BIN}/class_setup
source ${DEPLOY_BIN}/dgw_setup

echo $CLASSPATH 

set model = $2

set DGW_CONSOLE_LOG_FILE = $DEPLOY_TMP/dgw_${model}.log

# Add arg list.
source $DGW_CONFIG/${model}_dgw_args 
	
set genlist = `glob $GEN_LIST `

set PID = $DEPLOY_TMP/dgw_${model}.pid

switch ($1)

    case stop:

	echo "Data Generator Watchdog: (${model}) stopping"
	
	foreach gen ( $genlist )

	    # Launch each generator in bg mode.

	    $DEPLOY_BIN/gen stop $gen >>& ${DGW_CONSOLE_LOG_FILE} &

	end

	if (! -e $PID) then
	    echo "Cannot locate a PID file: $PID "
	    exit 1
	endif
	set AID = `cat $PID` 
    
	if ($AID == "") then
	    echo "$PID does not contain a valid PID for the DGW"
	    exit 2
	endif

	set BID = `ps -ef | grep $AID | awk '{if ($3 == '$AID') print $2}'`
	echo DGW Watchdog process is $AID ..Killing with SIG_KILL
	kill -9 $AID  
	echo DGW Process is          $BID ..Killing with SIG_KILL
	kill -9 $BID

	rm -f $PID

	breaksw

    case start:

	echo " Data Generator Watchdog: (${model}) starting"

	echo $$ > $PID

	set mexit = 0

	foreach gen ( $genlist )

	    # Launch each generator in bg mode.

	    $DEPLOY_BIN/gen start $gen >>& ${DGW_CONSOLE_LOG_FILE} &

	end

	breaksw

endsw
