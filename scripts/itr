#!/bin/csh

# -----------------------------------------
# Launcher for: Image Transfer Relay (ITR).
# -----------------------------------------

if (${#argv} < 1) then
    echo "\033[45mUsage: itr ( start | stop ) <scope> \033[0m"
    exit 1
endif

source $DEPLOY_BIN/setup
source $DEPLOY_BIN/class_setup
source $DEPLOY_BIN/itr_setup

# Add arg list.
source $ITR_CONFIG/${2}_itr_args

# Check certificates here......scripts/check_cert $KEY etc
# Select the certificates: (these can be relative to: IAS_HOME).

# Check we have the key store (server's private key).
if (! -e $KEY) then   
    echo "Could not find Keystore: $KEY "
    exit 2
endif

# Check we have the trust store (clients' public keys).
if (! -e $TRUST) then
    echo "Could not find Truststore: $TRUST "
    exit 3
endif

set APP = ngat.net.SSLFileTransfer

set EGD = -Djava.security.egd=file:/dev/urandom
#set EGD

#set DEBUG = -Djavax.net.debug=all
set DEBUG

set PID = $DEPLOY_TMP/itr_${2}.pid

switch ($1)

    case stop:

	echo "SSL Image Transfer Relay stopping"
	
	if (! -e $PID) then
	    echo "Cannot locate a PID file: $PID "
	    exit 1
	endif
	set AID = `cat $PID` 
    
	if ($AID == "") then
	    echo "$PID does not contain a valid PID for the IAS"
	    exit 2
	endif

	set BID = `ps -ef | grep $AID | awk '{if ($3 == '$AID') print $2}'`
	echo ITR Watchdog process is $AID ..Killing with SIG_KILL
	kill -9 $AID  
	echo ITR JVM Process is      $BID ..Killing with SIG_KILL
	kill -9 $BID

	breaksw

    case start:

	echo "SSL Image Transfer Relay starting"

	echo $$ > $PID
	
	set exit = 0

	$JAVA -DITR -Dcpdelim=${DD} $DEBUG $EGD $APP ${DD}id $ID $SERVER $RELAY $AUTH ${DD}port $PORT ${DD}base $BASE ${DD}key $KEY ${DD}trust $TRUST @log-level 3 @con-log-level 3
	set exit = $status 

	echo ITR Process exited with status $exit

	rm -f $PID

	breaksw

endsw






